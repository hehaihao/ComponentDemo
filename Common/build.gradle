apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'

Properties properties = new Properties()
def writer = rootProject.file('blockuser_config.properties')
properties.load(writer.newDataInputStream())

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        //公共配置
        commons{
            buildConfigField("String", "WX_APPID", properties.getProperty("WX_APPID"))
            buildConfigField("String", "WX_APP_SECRET", properties.getProperty("WX_APP_SECRET"))
            buildConfigField("String", "UMENG_APPKEY", properties.getProperty("UMENG_APPKEY"))
            matchingFallbacks = ['debug','release']
        }
        release.initWith(buildTypes.commons)
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "HTTP_BASE_URL", properties.getProperty("HTTP_BASE_RELEASE"))
            buildConfigField("String", "HTTP_TRACE_BASE_URL", properties.getProperty("HTTP_TRACE_BASE_RELEASE"))
            buildConfigField("String", "URL_HEAD_CHIP", properties.getProperty("URL_HEAD_RELEASE_CHIP"))
            buildConfigField("String", "URL_HEAD_CHIP_TRACE", properties.getProperty("URL_HEAD_CHIP_TRACE"))
            buildConfigField("String", "TRACE_H5_URL", properties.getProperty("TRACE_H5_BASE_URL_RELEASE"))
            buildConfigField("String", "URL_HEAD_QRCODE", properties.getProperty("URL_HEAD_QRCODE_RELEASE"))
            buildConfigField("String", "BROWSER_URL", properties.getProperty("BROWSER_URL_RELEASE"))
        }

        //开发运行用
        debug.initWith(buildTypes.commons)
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField("String", "HTTP_BASE_URL", properties.getProperty("HTTP_BASE_DEBUG"))
            buildConfigField("String", "HTTP_TRACE_BASE_URL", properties.getProperty("HTTP_TRACE_BASE_DEBUG"))
            buildConfigField("String", "URL_HEAD_CHIP", properties.getProperty("URL_HEAD_DEBUG_CHIP"))
            buildConfigField("String", "URL_HEAD_CHIP_TRACE", properties.getProperty("URL_HEAD_CHIP_TRACE"))
            buildConfigField("String", "TRACE_H5_URL", properties.getProperty("TRACE_H5_BASE_URL_DEBUG"))
            buildConfigField("String", "URL_HEAD_QRCODE", properties.getProperty("URL_HEAD_QRCODE_DEBUG"))
            buildConfigField("String", "BROWSER_URL", properties.getProperty("BROWSER_URL_DEBUG"))
        }
    }

    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api 'com.alibaba:arouter-api:1.4.0'
    // arouter-compiler 的注解依赖需要所有使用 ARouter 的 module 都添加依赖
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.1'

    //butterknife
    api 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    //retrofit和OKhttp配置
    api 'com.squareup.retrofit2:retrofit:2.8.1'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.8.1'
    api 'io.reactivex.rxjava2:rxandroid:2.1.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    api 'com.squareup.retrofit2:converter-scalars:2.8.1'

    api 'com.github.bumptech.glide:glide:3.7.0'
    api 'jp.wasabeef:glide-transformations:2.0.1'
    //okSocket
    api 'com.tonystark.android:socket:4.2.3'

    api "org.greenrobot:eventbus:3.1.1"

    api 'androidx.recyclerview:recyclerview:1.1.0'
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-21'

    api 'com.github.tbruyelle:rxpermissions:0.10.2'
    api 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    api 'androidx.multidex:multidex:2.0.1'
}